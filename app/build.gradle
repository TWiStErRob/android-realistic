apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/android-setup.gradle')

android {
	defaultConfig {
		applicationId "net.twisterrob.real"
		versionCode 1
		versionName "1.0"

		multiDexEnabled true // [multidex]

		testInstrumentationRunner 'net.twisterrob.real.test.instrumentation.AndroidJUnitRunner'
		testApplicationId 'net.twisterrob.real.test'

		buildConfigField("boolean", "USE_TEST_BUTLER", "false") // [test-butler]
	}

	buildTypes {
		// android:usesCleartextTraffic="${usesCleartextTraffic}"
		debug {
			applicationIdSuffix ".debug"
			// TODO Debug build is used for androidTest and MockWebServer is not HTTPS yet
			addManifestPlaceholders usesCleartextTraffic: true
		}
		release {
			addManifestPlaceholders usesCleartextTraffic: false
		}
	}
}

dependencies { // production

	implementation project(':component:configuration')

	compileOnly project(':feature:base')
	implementation project(':feature:about')
	implementation project(':feature:main')
	implementation project(':feature:repos')
	implementation project(':feature:repo')
	implementation fileTree(dir: 'libs', include: '*.jar')
	implementation "androidx.multidex:multidex:${VERSION_MULTIDEX}" // [multidex]
	implementation "androidx.appcompat:appcompat:${VERSION_APPCOMPAT}"

	kapt "com.google.dagger:dagger-compiler:${VERSION_DAGGER}"
	implementation "com.google.dagger:dagger:${VERSION_DAGGER}"

	// [dagger-android]
	implementation "com.google.dagger:dagger-android:${VERSION_DAGGER}"
	implementation "com.google.dagger:dagger-android-support:${VERSION_DAGGER}"
	kapt "com.google.dagger:dagger-android-processor:${VERSION_DAGGER}"

	// [stetho]
	debugImplementation "com.facebook.stetho:stetho:${VERSION_STETHO}"
	debugImplementation "com.facebook.stetho:stetho-okhttp3:${VERSION_STETHO}"

	implementation "com.squareup.okhttp3:okhttp:${VERSION_OKHTTP}"
}

dependencies { // UI tests
	androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${VERSION_KOTLIN}"
	androidTestImplementation project(':component:ui_test')
	androidTestImplementation project(':component:ui_test:helpers')

	androidTestImplementation "androidx.test:rules:${VERSION_ANDROID_TEST_RULES}"
	androidTestImplementation "androidx.test.ext:junit:${VERSION_ANDROID_TEST_RUNNER}"
	androidTestImplementation "com.linkedin.testbutler:test-butler-library:${VERSION_TEST_BUTLER}" // [test-butler]
	androidTestImplementation "com.squareup.rx.idler:rx1-idler:0.9.1"
}

// [big-gen] add task and specify where to put the .jar file
//noinspection UnnecessaryQualifiedReference inline for self-containedness
//task('packageBigClasses', type: net.twisterrob.real.gradle.GenerateBigClasses) {
//	preBuild.dependsOn it
//	destinationDir file('libs')
//	packageName 'net.twisterrob.real.gen'
//	baseName 'BigClasses'
//}
